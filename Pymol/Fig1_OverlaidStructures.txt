# Pymol scripts

###---Figure 1---
#get structures
fetch 2C3Z
fetch 1I4N
fetch 1VC4

#rename objects
set_name 2C3Z, SsIGPS
set_name 1I4N, TmIGPS
set_name 1VC4, TtIGPS

#remove alpha00 and chain b
remove (TmIGPS and resi 1-31)
remove (TtIGPS and resi 1-34)
remove (TmIGPS and chain B)
remove (TtIGPS and chain B)
remove solvent 
remove resn So4
remove resn ACY

#show as cartoon
set cartoon_loop_radius, 0.35
bg_color white
hide everything
show cartoon
color wheat

#align to SsIGPS
align TmIGPS, SsIGPS, cycles=0
align TtIGPS, SsIGPS, cycles=0
orient

#select libraries
select SsLib, SsIGPS AND resi 44-53+76-85+104-113+126-135+154-163+173-182+203-212+226-235
select TmLib, TmIGPS AND resi 40-49+74-83+102-111+124-133+150-159+172-181+202-211+224-233
select TtLib, TtIGPS AND resi 44-53+78-87+106-115+128-137+153-162+174-183+207-216+229-238
color orange, (SsLib | TmLib | TtLib) 

#set bg_color white
rotate z, 165
rotate x, -45

#create high quality image, save image
ray 2400, 2400
png Fig1a-Aligned-3-orthologs.png

#save session 
save alignedIGPS.pse


##---Fig 1b----
load alignedIGPS.pse

hide cartoon, SsIGPS
hide cartoon, TtIGPS
set cartoon_loop_radius, 0.35

sele Tmbaba2, TmIGPS and resi 102-149
hide cartoon, !Tmbaba2
zoom Tmbaba2, complete=1

rotate z, -35
rotate x, -70
rotate y, -90
rotate z, 10

#colorcode odd and even 
sele Tmhelix4, resi 133-149 and TmIGPS
color gray70, Tmhelix4

#hairpin
dist Tmhairpin3, (resi 126 AND TmIGPS), (resi 105 AND TmIGPS), mode=2
color blue, Tmhairpin3
hide labels, Tmhairpin3
show sticks, resi 105+126 AND TmIGPS #D position
set cartoon_side_chain_helper, 1 
ray 2400, 2400 
png Fig1b-4foldsymmetry-hairpin.png

ray 2400, 2400
png Fig1b-Four-fold-symmetry.png